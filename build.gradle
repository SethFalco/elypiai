buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
	}
}

plugins {
	id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'

String projectName = 'Elypiai'
String projectVersion = '1.0.0'

jar {
	from sourceSets.main.allSource

	manifest {
		attributes 	'Implementation-Title'	 : projectName,
					'Implementation-Version' : projectVersion
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	configurations = ['archives']

	pkg {
		repo = projectName
		name = projectName
		userOrg = 'Elypia'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://gitlab.com/Elypia/Elypiai.git'

		version {
			name = projectVersion
			desc = 'All in one API wrapper.'
			released = new Date()
		}
	}
}

repositories {
	jcenter()
}

dependencies {
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
	implementation group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
	implementation group: 'org.json', name: 'json', version: '20180130'

	runtimeOnly group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
	runtimeOnly group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.5'
	runtimeOnly group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.3'

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.0'

	testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.0'
	testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.1.0'
}

compileJava {
	options.compilerArgs += '-Xlint:unchecked'
}

junitPlatform {
	details 'tree'
}

task wrapper(type: Wrapper) {
	description = 'Generates gradlew[.bat] scripts'
	gradleVersion = '4.6'
}
