plugins {
    id "com.jfrog.bintray" version "1.8.4" apply false
}

allprojects() {
    apply plugin: "idea"
    apply plugin: "jacoco"

    version = "2.1.5"
    description = "Wrappers for many APIs available online from various services that don't provide official wrappers."

    ext {
        repoUrl = "https://gitlab.com/Elypia/elypiai.git"
        retrofitVersion = "2.6.0"
    }

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = "0.8.4"
    }
}

subprojects { subproject ->
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    sourceCompatibility = 11
    targetCompatibility = 11

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    jar {
        from sourceSets.main.allSource

        manifest {
            attributes "Implementation-Title": "Elypiai",
                       "Implementation-Version": version
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir

        javadoc {
            options.addBooleanOption("html5", true)
        }
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_KEY")
        publications = [this.name]
        publish = true

        pkg {
            repo = this.name
            name = subproject.name
            userOrg = "elypia"
            licenses = ["Apache-2.0"]
            vcsUrl = repoUrl

            version {
                name = version
                desc = description
                released = new Date()
            }
        }
    }

    publishing {
        def details = {
            licenses{
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution this.name
                }
            }

            developers{
                developer {
                    id "Seth"
                    name "Seth"
                    email "seth@elypia.com"
                }
            }

            scm{
                url repoUrl
            }
        }

        publications {
            Elypiai(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId "com.elypia.elypiai"
                artifactId subproject.name
                version version

                pom.withXml {
                    def root = asNode()

                    root.appendNode("description", description)
                    root.appendNode("name", this.name)
                    root.appendNode("url", "https://elypia.com/")
                    root.children().last() + details
                }
            }
        }
    }

    dependencies {
        // HTTP Requests
        implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"

        // Logging
        implementation "org.slf4j:slf4j-api:1.7.26"
        testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"

        // Testing
        testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
        testImplementation "org.mockito:mockito-core:2.28.2"
        testImplementation "com.squareup.okhttp3:mockwebserver:3.12.0"
    }

    /** Run tests */

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport

        reports {
            html.enabled = false
        }
    }
}

/** Generate Global JavaDocs for GitLab Pages */

task globalJavadoc(type: Javadoc) {
    source = subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

/** Combine and ouput code coverage. */

task jacocoMerge(type: JacocoMerge) {
    executionData = files(subprojects.jacocoTestReport.executionData.findAll {it.asList() })

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}

task jacocoReport(type: JacocoReport, dependsOn: jacocoMerge) {
    getSourceDirectories().setFrom(files(subprojects.sourceSets.main.allSource.srcDirs.findAll { it.asList() }))
    getClassDirectories().setFrom(files(subprojects.sourceSets.main.output.findAll { it.asList() }))
    executionData = files(jacocoMerge.destinationFile)

    reports {
        html.enabled = true
        xml.enabled = false
    }
}
