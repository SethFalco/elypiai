/*
 * Copyright 2019-2020 Elypia CIC and Contributors (https://gitlab.com/Elypia/elypiai/-/graphs/master)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

allprojects() {
    apply plugin: "jacoco"

    group = "org.elypia.elypiai"
    version = "4.3.0"
    description = "Wrappers for various web APIs that don't provide official libraries for Java."

    ext {
        retropiaVersion = "2.2.1"
        retrofitVersion = "2.9.0"
        okhttpVersion = "3.14.9"
    }

    repositories {
        mavenCentral()

        // Local System
        mavenLocal()
    }

    jacoco {
        toolVersion = "0.8.8"
    }
}

subprojects() {
    apply plugin: "java-library"

    sourceCompatibility = 17
    targetCompatibility = 17

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    dependencies {
        // Retropia (Retrofit) to Make Requests
        api("org.elypia.retropia:core:${retropiaVersion}")

        // RxJava for Handling Responses
        api("com.squareup.retrofit2:adapter-rxjava3:${retrofitVersion}")

        // Logging
        implementation("org.slf4j:slf4j-api:1.7.31")
        testRuntimeOnly("ch.qos.logback:logback-classic:1.2.3")

        // Testing
        testImplementation("org.junit.jupiter:junit-jupiter:5.7.2")
        testImplementation("org.elypia.webserver-testbed:junit5:1.0.1")
    }

    /** Testing */
    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport

        reports {
            html.enabled = false
        }
    }

    /** Code Coverage */
    jacocoTestReport {
        reports {
            html.enabled = false
            xml.enabled = false
        }
    }
}
