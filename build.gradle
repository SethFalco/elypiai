plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

allprojects() {
    apply plugin: "idea"
    apply plugin: "jacoco"

    ext {
        projectVersion = "2.1.4"
        projectDescription = "Wrappers for many APIs available online from various services."
        projectGit = "https://gitlab.com/Elypia/Elypiai.git"

        retrofitVersion = "2.5.0"
        jaxbApiVersion = "2.3.1"
        jaxbImplVersion = "2.3.1"
        jaxbCoreVersion = "2.3.0.1"
        javaxActivationVersion = "1.1.1"
        junitVersion = "5.3.2"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = "0.8.2"
    }
}

subprojects { subproject ->
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    jar {
        from sourceSets.main.allSource

        manifest {
            attributes "Implementation-Title"   : this.name,
                       "Implementation-Version" : projectVersion
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir

        javadoc {
            options.addBooleanOption("html5", true)

            options {
                links += [
                    "https://docs.oracle.com/javase/10/docs/api/"
                ]
            }
        }
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_KEY")
        publications = [this.name]
        publish = true

        pkg {
            repo = this.name
            name = subproject.name
            userOrg = "elypia"
            licenses = ["Apache-2.0"]
            vcsUrl = projectGit

            version {
                name = projectVersion
                desc = projectDescription
                released = new Date()
            }
        }
    }

    publishing {
        def details = {
            licenses{
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution this.name
                }
            }

            developers{
                developer {
                    id "Seth"
                    name "Seth"
                    email "seth@elypia.com"
                }
            }

            scm{
                url projectGit
            }
        }

        publications {
            Elypiai(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId "com.elypia.elypiai"
                artifactId subproject.name
                version projectVersion

                pom.withXml {
                    def root = asNode()

                    root.appendNode("description", projectDescription)
                    root.appendNode("name", this.name)
                    root.appendNode("url", "https://elypia.com/")
                    root.children().last() + details
                }
            }
        }
    }

    dependencies {
        // HTTP Requests
        implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"

        // Logging
        implementation "org.slf4j:slf4j-api:1.7.25"
        testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"

        // Testing
        testImplementation "com.squareup.okhttp3:mockwebserver:3.12.0"

        // Testing
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeOnly "org.junit.platform:junit-platform-runner:1.3.2"
    }

    test {
        useJUnitPlatform()

        reports {
            html.enabled = false
        }
    }
}

/**
 * Generate Global JavaDocs for GitLab Pages
 */

task globalJavadoc(type: Javadoc) {
    source = subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

/**
 * Test reporting and code coverage to Codacy.
 */

configurations {
    codacy
}

repositories {
    maven {
        url "https://jitpack.io"
    }

    maven {
        url "http://dl.bintray.com/typesafe/maven-releases"
    }
}

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

task jacocoMerge(type: JacocoMerge) {
    executionData = files(subprojects.jacocoTestReport.executionData.findAll {it.asList() })

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}

task jacocoReport(type: JacocoReport, dependsOn: jacocoMerge) {
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs.findAll { it.asList() })
    classDirectories = files(subprojects.sourceSets.main.output.findAll { it.asList() })
    executionData = files(jacocoMerge.destinationFile)

    reports {
        html.enabled = false
        xml.enabled = true
    }
}

task codacyCoverage(type: JavaExec, dependsOn: jacocoReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy

    args = [
        "report", "-l", "Java", "-r", "${buildDir}/reports/jacoco/jacocoReport/jacocoReport.xml"
    ]
}
